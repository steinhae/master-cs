language ESSENCE' 1.0
letting   RANGE be domain int(4,7,10,12)
letting   COLUMNS be domain int(0..7)   $mum = 0, brother = 1, grandma = 2, grandpa = 3, cousin = 4, aunt = 5, uncle = 6, so = 7
letting   mum=0
letting	  brother=1
letting	  grandma=2
letting	  grandpa=3
letting	  cousin=4
letting	  aunt=5
letting	  uncle=6
letting	  so=7

find      people: matrix indexed by [COLUMNS] of RANGE

such that

	$1 Assured by design

	$2 You have a total budget of 52 DM
	(sum(people) <= 52),

	$3 Your mum, brother and significant other must get different presents
	(people[mum] != people[brother]),
	(people[brother] != people[so]),
	(people[mum] != people[so]),
	
	$4 Your uncle and cousin must get different presents
	(people[uncle] != people[cousin]),

	$5 Your grandparents must get different presents
	(people[grandma] != people[grandpa]),
	
	$6 Your mum and uncle must get different presents
	(people[mum] != people[uncle]),
	
	$7 You cannot get chocolates for your mum, brother, significant other or grandpa
	(people[mum] != 4 /\ people[brother] != 4 /\ people[so] != 4 /\ people[grandpa] != 4),
	
	$8 Your cousin must not get socks
	(people[cousin] != 7),
	
	$9 There is only one coffee mill
	forAll a,b : int(0..7) . 
		((people[a] = 12 /\ people[b] = 12) -> (a = b)),
	
	$10 There are no more than 2 CDs
	forAll a,b,c : int(0..7) . 
		((people[a] = 10 /\ people[b] = 10 /\ people[c] = 10) -> (a = b \/ b = c \/ a = c)),
	
	$11 There are no more than 4 boxes of chocolates
	forAll a,b,c,d,e : int(0..7) .
		((people[a] = 4 /\ people[b] = 4 /\ people[c] = 4 /\ people[d] = 4 /\ people[e] = 4 ) -> (a = b \/ b = c \/ a = c \/ c = d \/ d = e \/ a = e \/ b = e \/ a = d \/ b = d \/ c = e)),
	
	$12 Your brother must get a CD
	(people[brother] = 10)

	
	